AC_INIT(README)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(aspnetedit, 0.0.1.0)
AM_MAINTAINER_MODE

AC_PROG_INSTALL
AC_PROG_CXX

### Dependency version numbers

MONO_REQUIRED_VERSION=1.1.9
GTKSHARP_REQUIRED_VERSION=2.5.5.99
GECKOSHARP_REQUIRED_VERSION=0.10

GTK_VERSION=2.6.0
MOZILLA_VERSION=1.6


### Check for pkg-config
AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
if test "x$PKG_CONFIG" = "xno"; then
	AC_MSG_ERROR([You need to install pkg-config])
fi

### Check mono version
PKG_CHECK_MODULES(MONO, mono >= $MONO_REQUIRED_VERSION)

#Check for C# compiler
AC_PATH_PROG(CSC, csc, no)
AC_PATH_PROG(MCS, mcs, no)
AC_PATH_PROG(MONO, mono, no)

### Find a c# compiler 

CS="C#"
if test "x$CSC" = "xno" -a "x$MCS" = "xno"  ; then
	dnl AC_MSG_ERROR([You need to install a C# compiler])
	AC_MSG_ERROR([No $CS compiler found])
fi

if test "x$MCS" = "xno" ; then
	MCS=$CSC
fi

if test "x$MONO" = "xno"; then
	AC_MSG_ERROR([No mono runtime found])
fi

AC_SUBST(MCS)

MCS_OPTIONS="-d:TRACE"
AC_SUBST(MCS_OPTIONS)


### Check c# dependencies

PKG_CHECK_MODULES(GTKSHARP, gtk-sharp-2.0 >= $GTKSHARP_REQUIRED_VERSION)
PKG_CHECK_MODULES(GECKOSHARP, gecko-sharp-2.0 >= $GECKOSHARP_REQUIRED_VERSION)

### Check c++ dependencies   
PKG_CHECK_MODULES(JSGLUEDEPS, mozilla-xpcom >= $MOZILLA_VERSION gtk+-2.0 >= $GTK_VERSION mozilla-gtkmozembed >= $MOZILLA_VERSION)

### Find the Mozilla directory to install chrome
MOZILLA_HOME="`$PKG_CONFIG --variable=libdir mozilla-gtkmozembed`"
AC_SUBST(MOZILLA_HOME)

AC_MSG_CHECKING(for installed-chrome.txt)
INSTALLED_CHROME="$MOZILLA_HOME/chrome/installed-chrome.txt"
if test -f "$INSTALLED_CHROME"; then
	AC_MSG_RESULT(yes)
	AC_SUBST(INSTALLED_CHROME)
else
	AC_MSG_RESULT(no)
	AC_MSG_ERROR([Your Mozilla installation has an unexpected structure])
fi

### Create build directories, as tarballs seem to ignore them despite being in svn
AC_MSG_CHECKING(for build directory)
if test -d build ; then
	AC_MSG_RESULT(yes)
else
	AC_MSG_RESULT(no)
	echo "Creating build directories..."
	mkdir build
	mkdir build/lib
fi



AC_OUTPUT([
Makefile
src/Makefile
src/propertygrid/Makefile
src/propertygrid/AssemblyInfo.cs
src/jscall/Makefile
src/jscall/AssemblyInfo.cs
src/jscall/jscallglue/Makefile
src/editor/Makefile
src/editor/AssemblyInfo.cs
src/aspnetedit/Makefile
src/aspnetedit/AssemblyInfo.cs
src/chrome/Makefile
])

echo ""
echo "Configuration summary"
echo ""
echo "   * Installation prefix = $prefix"
echo "   * C# compiler = $MCS"
echo "   * C# compiler options = $MCS_OPTIONS"
echo "   * Mozilla home: $MOZILLA_HOME"
echo ""
